Program.Sub.ScreenSU.Start
Gui.Form..create
Gui.Form..caption("WO in Progress Maintenance")
Gui.Form..size(12630,7710)
Gui.Form..minx(0)
Gui.Form..miny(0)
Gui.Form..position(0,0)
Gui.Form..event(UnLoad,Form_UnLoad)
Gui.Form..forecolor(0)
Gui.Form..BackColor(-2147483633)
Gui.Form..maxbutton(False)
Gui.Form..mousepointer(0)
Gui.Form..sizeable(False)
Gui.Form.lbl4.create(label,"Group",True,1170,255,1,1125,165,True,0,Arial,8,-2147483633,0)
Gui.Form.lbl4.defaultvalue("")
Gui.Form.lbl5.create(label,"Shift",True,570,255,1,3750,180,True,0,Arial,8,-2147483633,0)
Gui.Form.lbl5.defaultvalue("")
Gui.Form.cmdAdd.create(button)
Gui.Form.cmdAdd.caption("Add")
Gui.Form.cmdAdd.size(855,375)
Gui.Form.cmdAdd.position(90,810)
Gui.Form.cmdAdd.event(Click,cmdAdd_Click)
Gui.Form.cmdAdd.defaultvalue("")
Gui.Form.cmdAdd.tabstop(True)
Gui.Form.cmdAdd.tabindex(5)
Gui.Form.ddlShift.Create(DropDownList)
Gui.Form.ddlShift.Size(690,330)
Gui.Form.ddlShift.Position(3750,390)
Gui.Form.ddlShift.TabStop(True)
Gui.Form.ddlShift.TabIndex(3)
Gui.Form.ddlGroup.Create(DropDownList)
Gui.Form.ddlGroup.Size(2535,330)
Gui.Form.ddlGroup.Position(1125,390)
Gui.Form.ddlGroup.TabStop(True)
Gui.Form.ddlGroup.TabIndex(2)
Gui.Form.gsSel.Create(GsGridControl)
Gui.Form.gsSel.Size(12165,4710)
Gui.Form.gsSel.Position(90,1290)
Gui.Form.gsSel.TabStop(True)
Gui.Form.gsSel.TabIndex(8)
Gui.Form.gsSel.Event(RowCellClick,gsSel_RowCellClick)
Gui.Form.cmdSave.Create(Button)
Gui.Form.cmdSave.Size(855,375)
Gui.Form.cmdSave.Position(90,6870)
Gui.Form.cmdSave.TabStop(True)
Gui.Form.cmdSave.TabIndex(7)
Gui.Form.cmdSave.Caption("Save")
Gui.Form.cmdSave.DefaultValue("")
Gui.Form.cmdSave.Event(Click,cmdSave_Click)
Gui.Form.ddlDept.Create(DropDownList)
Gui.Form.ddlDept.Size(930,330)
Gui.Form.ddlDept.Position(90,390)
Gui.Form.ddlDept.TabStop(True)
Gui.Form.ddlDept.TabIndex(1)
Gui.Form.ddlDept.Event(Click,ddlDept_Click)
Gui.Form.lbl6.Create(Label,"Department",True,1935,255,1,90,180,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl6.DefaultValue("")
Gui.Form.txtRefreshTime.Create(TextBox,"120",True,840,300,0,90,6420,True,1,"Arial",8,-2147483643,1)
Gui.Form.txtRefreshTime.TabStop(True)
Gui.Form.txtRefreshTime.TabIndex(6)
Gui.Form.txtRefreshTime.NumericOnly(2)
Gui.Form.lbl1.Create(Label,"Refresh Time (secs)",True,1665,255,0,90,6180,True,0,"Arial",8,-2147483633,0)
Gui.Form.ddlScroll.Create(DropDownList)
Gui.Form.ddlScroll.Size(690,330)
Gui.Form.ddlScroll.Position(4515,390)
Gui.Form.ddlScroll.TabStop(True)
Gui.Form.ddlScroll.TabIndex(4)
Gui.Form.lbl7.Create(Label,"Scroll",True,570,255,1,4500,180,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl7.DefaultValue("")
Gui.Form.txteMail.Create(TextBox,"",False,3420,300,0,5340,405,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl8.Create(Label,"Supervisor EMail ID",False,2175,255,1,5325,180,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl8.DefaultValue("")
Gui.Form.lbl9.Create(Label,"Additional EMail ID",False,2175,255,1,8850,180,True,0,"Arial",8,-2147483633,0)
Gui.Form.lbl9.DefaultValue("")
Gui.Form.txteMail2.Create(TextBox,"",False,3420,300,0,8850,405,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl2.Create(Label,"Sender Email ID",True,1935,255,0,2265,6165,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtSendEmail.Create(TextBox,"",True,3390,300,0,2295,6420,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl3.Create(Label,"HTML Location",True,1935,255,0,5880,6150,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtHTMLLocation.Create(TextBox,"",True,3390,300,0,5910,6405,True,0,"Arial",8,-2147483643,1)
Gui.Form.lbl10.Create(Label,"Efficieny Goal",True,1935,255,0,9540,6150,True,0,"Arial",8,-2147483633,0)
Gui.Form.txtGoal.Create(TextBox,"",True,540,300,0,9570,6405,True,0,"Arial",8,-2147483643,1)
Gui.Form.txtGoal.MaxLength(2)
Gui.Form.txtGoal.NumericOnly(2)
Gui.Form.chknotification.Create(CheckBox)
Gui.Form.chknotification.Size(1605,360)
Gui.Form.chknotification.Position(1125,840)
Gui.Form.chknotification.Caption("Send Notification")
Gui.Form.chknotification.Event(Click,EmailAvailability)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
v.Global.sFolder.Declare
v.Global.sSender.Declare
v.Global.sFolderFile.Declare
v.Global.sSenderFile.Declare
v.Global.sGoalFile.Declare
v.Global.fGoal.Declare
Program.Sub.Preflight.End

Program.Sub.Main.Start
v.Local.sGlobalFile.declare
v.Local.bEx.Declare
v.Local.bcheck.Declare
v.Local.sSenderFile.Declare
v.Local.sGoalFile.Declare
'BN, Dec 27, 2018
'This project will create trueview web pages that is similar with the screen: On Line System > View > Current Work in Progress (No Dollars)
'User will maintain the selection in this script: Department + Employee Group (Employee_MSTR.Pr_Balance_Group) + Shift + Scroll(table body scroll feature)
'This program will be run from On Line System > Administration > Maintenance - Current WO in Progress Trueview - 5483
'changed it for life line 6071

f.Intrinsic.String.Build("{0}\CUSTOM",v.Caller.GlobalDir,v.Global.sFolder)
f.Intrinsic.File.DirExists(v.Global.sFolder,v.Local.bEx)
f.Intrinsic.Control.If(v.Local.bEx.Not)
	f.Intrinsic.File.CreateDir(v.Global.sFolder)
f.Intrinsic.Control.EndIf


f.Intrinsic.String.Build("{0}\ProjectFolder.txt",v.Caller.GasDir,v.Local.sGlobalFile)
v.Global.sFolderFile.Set(v.Local.sGlobalFile)
f.Intrinsic.File.Exists(v.Local.sGlobalFile,v.Local.bEx)
f.Intrinsic.Control.If(v.Local.bEx)
	f.Intrinsic.file.file2string(v.Local.sGlobalFile,v.Global.sFolder)
	f.Intrinsic.File.DirExists(v.Global.sFolder,v.Local.bCheck)
	f.Intrinsic.Control.If(v.Local.bCheck.Not)
		f.Intrinsic.String.Build("{0}\CUSTOM",v.Caller.GlobalDir,v.Global.sFolder)
		f.Intrinsic.file.String2File(v.Local.sGlobalFile,v.Global.sFolder)
	f.Intrinsic.Control.EndIf
f.Intrinsic.Control.Else
	f.Intrinsic.String.Build("{0}\CUSTOM",v.Caller.GlobalDir,v.Global.sFolder)
	f.Intrinsic.file.String2File(v.Local.sGlobalFile,v.Global.sFolder)
f.Intrinsic.Control.EndIf



gui.Form.txtHTMLLocation.Text(v.Global.sFolder)

f.Intrinsic.String.Build("{0}\SenderEmail.txt",v.Caller.GasDir,v.Local.sSenderFile)
v.Global.sSenderFile.Set(v.Local.sSenderFile)
f.Intrinsic.File.Exists(v.Local.sSenderFile,v.Local.bEx)
f.Intrinsic.Control.If(v.Local.bEx)
	f.Intrinsic.file.File2String(v.Local.sSenderFile,v.Global.sSender)
f.Intrinsic.Control.EndIf
gui.Form.txtSendEmail.Text(v.Global.sSender)

f.Intrinsic.String.Build("{0}\Goal.txt",v.Caller.GasDir,v.Local.sGoalFile)
v.Global.sGoalFile.Set(v.Local.sGoalFile)
f.Intrinsic.File.Exists(v.Local.sGoalFile,v.Local.bEx)
f.Intrinsic.Control.If(v.Local.bEx)
	
	f.Intrinsic.file.File2String(v.Local.sGoalFile,v.Global.fGoal)
f.Intrinsic.Control.Else
	v.Global.fGoal.Set(90)
	f.Intrinsic.file.String2File(v.Local.sGoalFile,v.Global.fGoal)
f.Intrinsic.Control.EndIf
gui.Form.txtGoal.Text(v.Global.fGoal)


f.Intrinsic.Control.Try
	
	v.Local..BulkDeclareString(sError,sRet)
	v.Local.iCnt.Declare	
	
	f.ODBC.Connection!conx.OpenCompanyConnection
	'Department dropdown
	gui.Form.ddlDept.AddItem("All")
	F.ODBC.Connection!conx.ExecuteAndReturn("SELECT DISTINCT ltrim(rtrim(DEPT_EMPLOYEE)) as Dept_Employee FROM EMPLOYEE_MSTR ORDER BY DEPT_EMPLOYEE",v.Local.sRet)
	f.Intrinsic.String.Split(v.Local.sRet,"#$#",v.Local.sRet)
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.Local.sRet.UBound,1)
		Gui.Form.ddlDept.AddItem(v.Local.sRet(v.Local.iCnt))
	f.Intrinsic.Control.Next(v.Local.iCnt)
	'Group dropdown
	gui.Form.ddlGroup.AddItem("All")
	F.ODBC.Connection!conx.ExecuteAndReturn("select ltrim(rtrim(Bal_Group)) + ' - ' + rtrim(Descr) as BalGroup from BALANCING_GROUP order by Bal_Group",v.Local.sRet)
	f.Intrinsic.String.Split(v.Local.sRet,"#$#",v.Local.sRet)
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.Local.sRet.UBound,1)
		Gui.Form.ddlGroup.AddItem(v.Local.sRet(v.Local.iCnt))
	f.Intrinsic.Control.Next(v.Local.iCnt)
	'Shift dropdown
	gui.Form.ddlShift.AddItem("All")
	F.ODBC.Connection!conx.ExecuteAndReturn("select Distinct shift as Shift from employee_shift order by shift",v.Local.sRet)
	f.Intrinsic.String.Split(v.Local.sRet,"#$#",v.Local.sRet)
	f.Intrinsic.Control.For(v.Local.iCnt,0,v.Local.sRet.UBound,1)
		Gui.Form.ddlShift.AddItem(v.Local.sRet(v.Local.iCnt))
	f.Intrinsic.Control.Next(v.Local.iCnt)	
	'Scroll
	gui.Form.ddlScroll.AddItem("Yes")
	gui.Form.ddlScroll.AddItem("No")
	'Set default list index
	gui.Form.ddlDept.ListIndex(0)
	gui.Form.ddlGroup.ListIndex(0)
	gui.Form.ddlShift.ListIndex(0)
	gui.Form.ddlScroll.ListIndex(0)
	f.Intrinsic.Control.CallSub("Bind_Grid")
	gui.Form..Show

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form_UnLoad.Start
f.Intrinsic.Control.CallSub("Unload")
Program.Sub.Form_UnLoad.End

Program.Sub.Unload.Start
f.Intrinsic.Control.Try
	V.Local.sError.Declare
	
	f.Intrinsic.Control.If(v.ODBC.conx.State,=,1)
		F.ODBC.Connection!conX.close
	f.Intrinsic.Control.EndIf
	
	F.Intrinsic.Control.End

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.End
F.Intrinsic.Control.EndTry
Program.Sub.Unload.End

Program.Sub.cmdAdd_Click.Start
f.Intrinsic.Control.Try
	V.Local.bCheck.Declare
	V.Local..BulkDeclareString(sError,sDept,sGroup,sShift,sSQL,sRet,sFilter)
'	
'	Function.Global.Messaging.IsAddressValid(v.Screen.Form!txteMail.text.trim,v.local.bCheck)
'	
'	f.Intrinsic.Control.If(v.Local.bCheck,=,False,"AND",v.Screen.Form!ddlDept.Text.Trim,<>,"All")
'		f.Intrinsic.UI.Msgbox("You need to enter a valid supervisor email ID.")		
'		f.Intrinsic.Control.ExitSub
'	f.Intrinsic.Control.EndIf
'	
'	f.Intrinsic.Control.If(v.Screen.Form!txteMail2.text.trim,<>,"")
'		Function.Global.Messaging.IsAddressValid(v.Screen.Form!txteMail2.text.trim,v.local.bCheck)
'		f.Intrinsic.Control.If(v.Local.bCheck.Not)
'			f.Intrinsic.UI.Msgbox("You need to enter a valid Additional email ID.")		
'			f.Intrinsic.Control.ExitSub
'		f.Intrinsic.Control.EndIf
'	f.Intrinsic.Control.EndIf
	
	'We only need to verify the email if the notification check is checked
	F.Intrinsic.Control.If(V.Screen.Form!chknotification.Value, =, 1)
		
		Function.Global.Messaging.IsAddressValid(v.Screen.Form!txteMail.text.trim,v.local.bCheck)
				
		F.Intrinsic.Control.If(v.Local.bCheck,=,False)
			f.Intrinsic.UI.Msgbox("You need to enter a valid supervisor email ID to make notifications available.")		
			f.Intrinsic.Control.ExitSub
		f.Intrinsic.Control.EndIf
		
		f.Intrinsic.Control.If(v.Screen.Form!txteMail2.text.trim,<>,"")
			Function.Global.Messaging.IsAddressValid(v.Screen.Form!txteMail2.text.trim,v.local.bCheck)
			f.Intrinsic.Control.If(v.Local.bCheck.Not)
				f.Intrinsic.UI.Msgbox("You need to enter a valid Additional email ID to make notifications available.")
				f.Intrinsic.Control.ExitSub
			f.Intrinsic.Control.EndIf
		f.Intrinsic.Control.EndIf
		
	F.Intrinsic.Control.EndIf
	
	
	'Change label to ALL. for avoiding SQL keyword All
	f.Intrinsic.Control.If(v.Screen.Form!ddlDept.Text.Trim,=,"All")
		v.Local.sDept.Set("AllDept")
	f.Intrinsic.Control.Else
		v.Local.sDept.Set(v.Screen.Form!ddlDept.Text.Trim)
	f.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.If(v.Screen.Form!ddlGroup.Text.Trim,=,"All")
		v.Local.sGroup.Set("AllGroup")
	f.Intrinsic.Control.Else
		f.Intrinsic.String.Split(v.Screen.Form!ddlGroup.Text.Trim,"-",v.Local.sGroup)
		v.Local.sGroup.Set(v.Local.sGroup(0).Trim)
	f.Intrinsic.Control.EndIf
	
	f.Intrinsic.Control.If(v.Screen.Form!ddlShift.Text.Trim,=,"All")
		v.Local.sShift.Set("AllShift")
	f.Intrinsic.Control.Else
		v.Local.sShift.Set(v.Screen.Form!ddlShift.Text.Trim)
	f.Intrinsic.Control.EndIf	
	
	f.Intrinsic.UI.InvokeWaitDialog("Adding...")
	'Check if Selection exists
	f.Intrinsic.String.Build("Dept = '{0}' and BalGroup = '{1}' and Shift = '{2}' and Scroll = '{3}'",v.Local.sDept,v.Local.sGroup,v.Local.sShift,v.Screen.Form!ddlScroll.Text.Trim,v.Local.sFilter)
	f.Data.DataView.Create("Sel","SelDV",22,v.Local.sFilter,"")
	f.Intrinsic.Control.If(v.DataView.Sel!SelDV.RowCount,>,0)
		f.Intrinsic.UI.Msgbox("Selection already exists.")
	f.Intrinsic.Control.Else
		f.Data.DataTable.AddRow("Sel","Dept",v.Local.sDept,"BalGroup",v.Local.sGroup,"Shift",v.Local.sShift,"RefreshTime",v.Screen.Form!txtRefreshTime.Text.Trim,"Scroll",v.Screen.Form!ddlScroll.Text.Trim,"EmailID",v.Screen.Form!txteMail.text.trim,"EmailID2",v.Screen.Form!txteMail2.text.trim,"Notify",V.Screen.Form!chknotification.Value,"Delete","Delete")
		gui.Form.txteMail.Text("")
		gui.Form.txteMail2.Text("")
		Gui.Form.chknotification.Value(False)
	f.Intrinsic.Control.EndIf
	f.Data.DataView.Close("Sel","SelDV")
	f.Intrinsic.UI.CloseWaitDialog

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry

Program.Sub.cmdAdd_Click.End

Program.Sub.cmdSave_Click.Start
f.Intrinsic.Control.Try
	v.Local..BulkDeclareString(sError)
	v.Local.bCheck.Declare
	f.Intrinsic.Control.If(v.Screen.Form!txtRefreshTime.Text,<,120)
		f.Intrinsic.UI.Msgbox("Minimum refresh time must be greater than 120")
		gui.Form.txtRefreshTime.Text("120")
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.EndIf
	
	Function.Global.Messaging.IsAddressValid(v.Screen.Form!txtSendEmail.text.trim,v.local.bCheck)
	f.Intrinsic.Control.If(v.Local.bCheck.Not)
		f.Intrinsic.UI.Msgbox("You need to enter a valid Sender email ID.")		
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.Else
		v.Global.sSender.Set(v.Screen.Form!txtSendEmail.text.trim)
		f.Intrinsic.File.String2File(v.Global.sSenderFile,v.Global.sSender)
	f.Intrinsic.Control.EndIf
	
	f.Intrinsic.File.DirExists(v.Screen.Form!txtHTMLLocation.text.trim,v.Local.bCheck)
	
	f.Intrinsic.Control.If(v.Local.bCheck.Not)
		f.Intrinsic.UI.Msgbox("You need to enter a valid HTML Folder Location.")		
		f.Intrinsic.Control.ExitSub
	f.Intrinsic.Control.Else
		v.Global.sFolder.Set(v.Screen.Form!txtHTMLLocation.text.trim)
		f.Intrinsic.File.String2File(v.Global.sFolderFile,v.Global.sFolder)
	f.Intrinsic.Control.EndIf
	
	v.Global.fGoal.Set(v.Screen.Form!txtGoal.text.float)
	f.Intrinsic.File.String2File(v.Global.sGoalFile,v.Global.fGoal)
	
	f.Intrinsic.UI.InvokeWaitDialog("Saving...")
	f.ODBC.Connection!conx.Execute("Delete from GCG_6071_Selection")
	f.Data.DataTable.SetValue("Sel",-1,"RefreshTime",v.Screen.Form!txtRefreshTime.Text.Trim)
	f.Data.DataTable.SaveToDB("Sel","conx","GCG_6071_Selection","",128,"Dept@!@Dept*!*BalGroup@!@BalGroup*!*Shift@!@Shift*!*RefreshTime@!@RefreshTime*!*Scroll@!@Scroll*!*EmailID@!@EmailID*!*EmailID2@!@EmailID2*!*Notify@!@Notify")
	f.Intrinsic.UI.CloseWaitDialog

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.Bind_Grid.Start
'Bind GCG_6071_Selection to the Grid
f.Intrinsic.Control.Try
	v.Local..BulkDeclareString(sError)
	
	f.Intrinsic.UI.InvokeWaitDialog("Binding...")
	f.Intrinsic.Control.If(v.DataTable.Sel.Exists)
		f.Data.DataTable.Close("Sel")		
	f.Intrinsic.Control.EndIf
	f.Data.DataTable.CreateFromSQL("Sel","conx","Select ltrim(rtrim(Dept)) as Dept,ltrim(rtrim(BalGroup)) as BalGroup,Shift,RefreshTime,rtrim(Scroll) as Scroll,rtrim(EmailID) as EmailID,rtrim(EmailID2) as EmailID2, Notify From GCG_6071_Selection order by Dept,BalGroup,Shift,RefreshTime",True)
	f.Data.DataTable.AddColumn("Sel","Delete","string","Delete")	
	gui.Form.gsSel.AddGridviewFromDatatable("GVMain","Sel")
	gui.Form.gsSel.SuspendLayout()
	gui.Form.gsSel.SetColumnProperty("GvMain","Delete","Width","30")
	gui.Form.gsSel.SetColumnProperty("GvMain","Shift","Width","30")
	gui.Form.gsSel.SetColumnProperty("GvMain","RefreshTime","Width","60")
	gui.Form.gsSel.SetColumnProperty("GvMain","Scroll","Width","25")
	gui.Form.gsSel.SetColumnProperty("GvMain","BalGroup","MaxWidth","60")
	gui.Form.gsSel.SetColumnProperty("GvMain","Dept","Width","60")
	gui.Form.gsSel.SetColumnProperty("GvMain","Notify","Width","60")
	gui.Form.gsSel.SetColumnProperty("GvMain","EmailID","MaxWidth","200")
	gui.Form.gsSel.SetColumnProperty("GvMain","EmailID","Caption","Supervisor Email ID")
	gui.Form.gsSel.SetColumnProperty("GvMain","EmailID2","MaxWidth","200")
	gui.Form.gsSel.SetColumnProperty("GvMain","EmailID2","Caption","Additional Email ID")
	gui.Form.gsSel.SetColumnProperty("GvMain","BalGroup","Caption","Group")
	gui.Form.gsSel.SetColumnProperty("GvMain","RefreshTime","Caption","Refresh Time")
	gui.Form.gsSel.SetColumnProperty("GvMain","RefreshTime","Visible",False)
	gui.Form.gsSel.SetColumnProperty("GvMain","Delete","ReadOnly",True)
	gui.Form.gsSel.SetColumnProperty("GvMain","Delete","AllowEdit",False)
	gui.Form.gsSel.ColumnEdit("GvMain","Delete","EditorButton","Delete")
	gui.Form.gsSel.SetColumnProperty("GvMain","Delete","HeaderHAlignment","Center")
	gui.Form.gsSel.ResumeLayout()
	gui.Form.gsSel.MainView("GvMain")
	f.Intrinsic.Control.If(v.DataTable.Sel.RowCount,>,0)
		gui.Form.txtRefreshTime.Text(v.DataTable.Sel(0).RefreshTime!FieldValString)
	f.Intrinsic.Control.EndIf

	f.Intrinsic.UI.CloseWaitDialog

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.Bind_Grid.End

Program.Sub.gsSel_RowCellClick.Start
f.Intrinsic.Control.Try
	v.Local..BulkDeclareString(sError)
	
	f.Intrinsic.Control.If(v.Args.Column,=,"Delete")
		f.Intrinsic.UI.InvokeWaitDialog("Deleting...")
		f.Data.DataTable.DeleteRow("Sel",v.Args.RowIndex)
		f.Data.DataTable.AcceptChanges("Sel")
		f.Intrinsic.UI.CloseWaitDialog		
	f.Intrinsic.Control.EndIf

F.Intrinsic.Control.Catch
	F.Intrinsic.String.Build("Project: {0}{1}Subroutine: {2}{1}Error Occurred {3} with description {4}{1}GAB Version: {5}}",V.Ambient.ScriptPath,V.Ambient.Newline,V.Ambient.CurrentSubroutine,V.Ambient.ErrorNumber,V.Ambient.ErrorDescription,V.Ambient.GABVersion,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
	f.Intrinsic.Control.CallSub("Unload")
F.Intrinsic.Control.EndTry
Program.Sub.gsSel_RowCellClick.End

Program.Sub.ddlDept_Click.Start
f.Intrinsic.Control.If(v.Screen.Form!ddlDept.Text.Trim,=,"")
	gui.Form.lbl8.Visible(false)
	gui.Form.lbl9.Visible(false)
	gui.Form.txteMail.Visible(false)
	gui.Form.txteMail2.Visible(false)
f.Intrinsic.Control.Else
	gui.Form.lbl8.Visible(true)
	gui.Form.lbl9.Visible(true)
	gui.Form.txteMail.Visible(true)
	gui.Form.txteMail2.Visible(true)
	
f.Intrinsic.Control.EndIf
Program.Sub.ddlDept_Click.End

Program.Sub.EmailAvailability.Start
F.intrinsic.Control.Try

'Check the value of the textbox and make the emails available if 
F.Intrinsic.Control.If(V.Screen.Form!chknotification.Value, =, 0)
	Gui.Form.lbl8.Visible(False)
	Gui.Form.lbl9.Visible(False)
	Gui.Form.txteMail.Visible(False)
	Gui.Form.txteMail2.Visible(False)

F.Intrinsic.Control.Else
	Gui.Form.lbl8.Visible(True)
	Gui.Form.lbl9.Visible(True)
	Gui.Form.txteMail.Visible(True)
	Gui.Form.txteMail2.Visible(True)

F.Intrinsic.Control.EndIf

F.intrinsic.Control.Catch
F.Intrinsic.Control.EndTry
Program.Sub.EmailAvailability.End
